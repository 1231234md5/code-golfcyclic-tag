#!/usr/bin/perl

use v5.26;
use warnings;

use Digest::MD5 'md5_base64';
use IPC::Open2 'open2';

open my $fh, '>', 'routes/assets.go';

print $fh <<GO;
package routes

import (
	"net/http"
	"strings"
)

func Asset(w http.ResponseWriter, r *http.Request) {
	switch r.URL.Path {
GO

my ( %data, %paths );

for (<assets/hole.js>) {
    say;

    my $data = slurp($_);

    s|^assets/||;

    my $name = s/[.-](\w)/\U$1/gr;

    $data =~ s|/\* include ([^ ]+) +\*/|slurp("assets/includes/$1")|eg;

    $data = run(
        $data, qw(java -jar /closure-compiler.jar --language_out ES_2017) );

    $data{"${name}Br"} = run($data, 'brotli');

    # Zopfli can't stream https://github.com/google/zopfli/issues/17
    open my $tmp, '>', '/tmp/zopfli';
    print $tmp $data;
    close $tmp;

    $data{"${name}Gz"} = `zopfli --i50 -c /tmp/zopfli`;

    my $path = $paths{$name} = '/assets/' . md5_base64($data) =~ y|+/|-_|r;

    print $fh <<GO;
	case ${name}Path:
		w.Header().Set("Cache-Control", "max-age=9999999,public")
		w.Header().Set("Content-Type", "application/javascript")

		if strings.Contains(r.Header.Get("Accept-Encoding"), "br") {
			w.Header().Set("Content-Encoding", "br")
			w.Write(${name}Br)
		} else {
			w.Header().Set("Content-Encoding", "gzip")
			w.Write(${name}Gz)
		}
GO
}

print $fh "\t}\n}\n\n";
print $fh qq/const ${_}Path = "$paths{$_}"\n/ for sort keys %paths;
print $fh "\n";

for ( sort keys %data ) {
    printf "%12s = %5d bytes\n", $_, length $data{$_};

    my $i = 0;
    my $data = join ',',
        map +( "\n" x !($i++ % 15) ) . "0x$_", unpack '(H2)*', $data{$_};

    print $fh "var $_ = []byte{$data}\n\n";
}

sub run {
    my ( $data, @cmd ) = @_;

    local $/;

    open2( my $out, my $in, @cmd );
    print $in $data;
    close $in;

    scalar <$out>;
}

sub slurp { local ( @ARGV, $/ ) = @_; scalar <> }
